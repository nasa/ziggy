// Show all dependencies.
task allDeps(type: DependencyReportTask) {}

// Generate the Ziggy version information.
import gov.nasa.ziggy.buildutil.ZiggyVersionGenerator

def gitVersion = new ByteArrayOutputStream()
exec {
    commandLine "git", "rev-parse", "HEAD"
    standardOutput = gitVersion
}
gitVersion = gitVersion.toString().trim()

task ziggyVersion(type: ZiggyVersionGenerator) {
    inputs.property "ziggyVersion", gitVersion
}

processResources.dependsOn ziggyVersion
compileTestJava.dependsOn processResources
integrationTest.dependsOn processResources
sourcesJar.dependsOn processResources

clean.doFirst() {
    File supervisorPidFile = new File("$buildDir/bin/supervisor.pid");
    if (supervisorPidFile.isFile()) {
        throw new GradleException("Can't clean while cluster is running (run \"ziggy cluster stop\" to clear build/bin/supervisor.pid)")
    }
}

// Some tests create readonly directories. Make them writable so they can be deleted.
task fixTestPermissions(type : Exec) {
    onlyIf { file("$buildDir/test").exists() }

    commandLine "chmod", "-R", "u+wx", "$buildDir/test"
}

clean.dependsOn fixTestPermissions
