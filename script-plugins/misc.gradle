// Show all dependencies.
task allDeps(type: DependencyReportTask) {}

def gitVersionProvider = providers.exec {
    commandLine "git", "rev-parse", "HEAD"
}

task ziggyVersion(type: JavaExec) {
    inputs.property "ziggyVersion", gitVersionProvider.standardOutput.asText.get().trim()
    outputs.file "$buildDir/etc/ziggy-build.properties"
    classpath = sourceSets.main.runtimeClasspath
    mainClass = "gov.nasa.ziggy.util.BuildInfo"
    args = ["--prefix", "ziggy", "--home", "$buildDir"]
}

integrationTest.dependsOn ziggyVersion
assemble.dependsOn ziggyVersion

clean.doFirst() {
    File supervisorPidFile = new File("$buildDir/bin/supervisor.pid");
    if (supervisorPidFile.isFile()) {
        throw new GradleException("Can't clean while cluster is running (run \"ziggy cluster stop\" to clear build/bin/supervisor.pid)")
    }
}

// Some tests create readonly directories. Make them writable so they can be deleted.
task fixTestPermissions(type : Exec) {
    onlyIf { file("$buildDir/test").exists() }

    commandLine "chmod", "-R", "u+wx", "$buildDir/test"
}

clean.dependsOn fixTestPermissions
